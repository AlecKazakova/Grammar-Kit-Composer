buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10"
    classpath "com.diffplug.spotless:spotless-plugin-gradle:6.4.1"
    classpath "com.vanniktech:gradle-maven-publish-plugin:0.19.0"
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:1.6.10"
  }
}

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'java-gradle-plugin'
apply plugin: "com.diffplug.spotless"

spotless {
  kotlin {
    target "**/*.kt"
    ktlint('0.41.0').userData(["indent_size": "2"])
    trimTrailingWhitespace()
    endWithNewline()
  }
}

gradlePlugin {
  plugins {
    grammarKitComposer {
      id = 'com.alecstrong.grammar.kit.composer'
      implementationClass = 'com.alecstrong.grammar.kit.composer.GrammarKitComposerPlugin'
    }
  }
}

repositories {
  mavenCentral()
  gradlePluginPortal()
}

dependencies {
  implementation "com.squareup:kotlinpoet:1.11.0"
  implementation "org.jetbrains.intellij.plugins:gradle-grammarkit-plugin:2021.2.2"

  compileOnly gradleApi()
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"

plugins {
  alias(libs.plugins.kotlinJvm)
  id 'java-gradle-plugin'
  alias(libs.plugins.spotless)
  alias(libs.plugins.publish) apply false
  alias(libs.plugins.dokka)
}

repositories {
  mavenCentral()
  gradlePluginPortal()
}

spotless {
  kotlin {
    target "**/*.kt"
    targetExclude "**/build/grammar/**/*.*", "**/generated/**/*.*", "src/test/fixtures/**/build/grammars/**/*.*"
    ktlint('0.49.1').editorConfigOverride([
      "indent_size": "2",
       "disabled_rules": "package-name",
       "ij_kotlin_allow_trailing_comma": "true",
       "ij_kotlin_allow_trailing_comma_on_call_site": "true",
    ])
    trimTrailingWhitespace()
    endWithNewline()
  }
}

gradlePlugin {
  plugins {
    grammarKitComposer {
      id = 'com.alecstrong.grammar.kit.composer'
      implementationClass = 'com.alecstrong.grammar.kit.composer.GrammarKitComposerPlugin'
    }
  }
}

dependencies {
  implementation libs.kotlinPoet
  implementation libs.grammarKitPlugin

  testImplementation gradleTestKit()
  testImplementation "org.jetbrains.kotlin:kotlin-test"
}

kotlin.jvmToolchain(17)

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"

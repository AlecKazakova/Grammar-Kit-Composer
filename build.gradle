plugins {
  alias(libs.plugins.kotlinJvm)
  id 'java-gradle-plugin'
  alias(libs.plugins.spotless)
  alias(libs.plugins.publish) apply false
  alias(libs.plugins.dokka)
}

repositories {
  mavenCentral()
  gradlePluginPortal()
}

spotless {
  kotlin {
    target "**/*.kt"
    ktlint('0.46.1').editorConfigOverride([
      "indent_size": "2",
       "disabled_rules": "package-name",
       "ij_kotlin_allow_trailing_comma": "true",
       "ij_kotlin_allow_trailing_comma_on_call_site": "true",
    ])
    trimTrailingWhitespace()
    endWithNewline()
  }
}

gradlePlugin {
  plugins {
    grammarKitComposer {
      id = 'com.alecstrong.grammar.kit.composer'
      implementationClass = 'com.alecstrong.grammar.kit.composer.GrammarKitComposerPlugin'
    }
  }
}

dependencies {
  implementation libs.kotlinPoet
  implementation libs.grammarKitPlugin
}

tasks.withType(JavaCompile).configureEach {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"
